import com.toggl.buildsrc.Libs
import com.toggl.buildsrc.Versions

buildscript {
    ext.buildConfig = [
            'compileSdk': 29,
            'minSdk'    : 21,
            'targetSdk' : 29,
            'versionName' : "1.0",
            'versionCode' : 600000,
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Test.junit5Plugin
        classpath Libs.gradleVersionsPlugin
        classpath Libs.Google.googleServicesPluginClassPath
        classpath Libs.Google.firebaseCrashlyticsPluginClassPath
        classpath Libs.Google.firebasePerformancePluginClassPath
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.28.0'
    id 'com.github.kt3k.coveralls' version '2.10.1'
}

apply plugin: 'com.github.ben-manes.versions'
defaultTasks 'dependencyUpdates'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(Versions.ktlint)
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true
            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}
